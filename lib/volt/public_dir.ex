defmodule Volt.PublicDir do
  @moduledoc """
  Vite-style public directory support.

  Files in the configured public directory are served from the site root in
  development and copied as-is to the production static root. They are not
  transformed, hashed, or included in the module graph, so application code
  should reference them with root-absolute URLs such as `/favicon.svg`.
  """

  @doc """
  Returns the expanded public directory path, or `nil` when disabled.
  """
  @spec resolve(String.t() | false | nil) :: String.t() | nil
  def resolve(false), do: nil
  def resolve(nil), do: nil
  def resolve(path), do: Path.expand(path)

  @doc """
  Copies all files from `public_dir` into `static_root`.

  Missing public directories are ignored. Existing files at the destination are
  overwritten, matching the usual behavior of build output generation.
  """
  @spec copy(String.t() | nil, String.t()) :: :ok
  def copy(nil, _static_root), do: :ok

  def copy(public_dir, static_root) do
    if File.dir?(public_dir) do
      public_dir
      |> files()
      |> Enum.each(&copy_file(&1, public_dir, static_root))
    end

    :ok
  end

  @doc """
  Resolves a root-relative request path inside `public_dir`.

  Returns `nil` for missing files, directories, disabled public directories, or
  paths that would escape the public directory.
  """
  @spec lookup(String.t() | nil, String.t()) :: String.t() | nil
  def lookup(nil, _request_path), do: nil

  def lookup(public_dir, request_path) do
    relative = request_path |> String.trim_leading("/") |> URI.decode()
    path = Path.expand(relative, public_dir)

    if inside?(path, public_dir) and File.regular?(path), do: path
  end

  defp files(public_dir) do
    public_dir
    |> Path.join("**/*")
    |> Path.wildcard(match_dot: true)
    |> Enum.reject(&File.dir?/1)
  end

  defp copy_file(path, public_dir, static_root) do
    relative = Path.relative_to(path, public_dir)
    dest = Path.join(static_root, relative)
    File.mkdir_p!(Path.dirname(dest))
    File.cp!(path, dest)
  end

  defp inside?(path, root), do: path == root or String.starts_with?(path, root <> "/")
end
