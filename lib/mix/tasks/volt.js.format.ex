defmodule Mix.Tasks.Volt.Js.Format do
  use Mix.Task

  @shortdoc "Format Volt TypeScript assets"

  @moduledoc """
  Format Volt's JavaScript and TypeScript assets with oxfmt via NIF.

      mix volt.js.format

  Reads options from `config :volt, :format`. Falls back to `.oxfmtrc.json`.
  File discovery uses `config :volt, sources:` and `ignore:`.
  No Node.js required.
  """

  @impl true
  def run(_args) do
    Mix.Task.run("app.config")

    files = Volt.JS.Helpers.discover_format_files()

    if files == [] do
      Mix.shell().info("No formattable files found")
    else
      %{changed: changed, total: total} = Volt.JS.Format.format_files(files)

      if changed == 0 do
        Mix.shell().info("All #{total} files already formatted")
      else
        Mix.shell().info("Formatted #{changed}/#{total} files")
      end
    end
  end
end
